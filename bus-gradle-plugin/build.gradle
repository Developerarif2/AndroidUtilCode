plugins {
    id 'groovy'
    id 'java-gradle-plugin' // for gradlePlugin DSL
    id 'com.gradle.plugin-publish' version "0.10.0" //for pluginPublish.gradle
}

apply {
    from "../gradle/pluginPublish.gradle"
    if (bus.isDebug) {
        plugin 'maven'
        from "../gradle/localMavenUpload.gradle"
    } else {
        plugin 'com.github.dcendents.android-maven'
        plugin 'com.jfrog.bintray'
        from "../gradle/bintrayUploadJava.gradle"
    }
}

gradlePlugin {
    plugins {
        busPlugin {
            id = 'com.blankj.bus'
            implementationClass = 'com.blankj.bus.BusPlugin'
        }
    }
}

dependencies {
    implementation dep.gradle
    implementation dep.javassist
    implementation dep.commons_io
    implementation gradleApi()
    implementation localGroovy()

    testImplementation dep.junit
}

sourceSets {
    main {
        groovy {
            srcDirs += 'src/main/java'
        }
    }

    test {
        groovy {
            srcDirs += 'src/test/java'
        }
    }
}

group = bus.group
version = bus.version

//./gradlew bus-gradle-plugin:bintrayUpload
//./gradlew publishPlugins
