// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        if (!gradle.ext.bus.isPublish) {
            if (gradle.ext.bus.isDebug) {
                maven() {
                    url uri(new File(project.rootDir, "maven"))
                }
            }
        }
        google()
        jcenter()
    }

    dependencies {
        for (plugin in gradle.ext.dep.plugin) {
            if (gradle.ext.bus.isPublish && plugin.contains("com.blankj:bus-gradle-plugin:")) {
                continue
            }
            classpath plugin
        }
    }
}
allprojects {
    repositories {
        google()
        jcenter()
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

        resolutionStrategy.eachDependency {
            if (it.requested.group == 'com.android.support'
                    && !it.requested.name.contains('multidex')) {
                it.useVersion gradle.ext.support_version
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
